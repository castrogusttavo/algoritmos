Algoritmo "sequencias_numericas"
// Disciplina   : [Linguagem e Lógica de Programação]
// Professor   : Antonio Carlos Nicolodi
// Descrição   : Geração de diversas sequências numéricas
// Autor(a)    : Nome do(a) aluno(a)
// Data atual  : 16/11/2024
Var
    length, i, idade: inteiro
    sequence: vetor[1..100] de inteiro
    opcoes: caractere

Procedimento fibonacciSequence(length: inteiro)
Var
    i: inteiro
Inicio
    sequence[1] <- 0
    sequence[2] <- 1
    para i de 3 ate length faca
        sequence[i] <- sequence[i-1] + sequence[i-2]
    fimpara
FimProcedimento

Procedimento fettuccineSequence(month, day, length: inteiro)
Var
    i: inteiro
Inicio
    sequence[1] <- month
    sequence[2] <- day
    para i de 3 ate length faca
        se (i % 2 = 0) entao
            sequence[i] <- sequence[i-2] - sequence[i-1]
        senao
            sequence[i] <- sequence[i-2] + sequence[i-1]
        fimse
    fimpara
FimProcedimento

Procedimento primeNumbersFromAge(age, length: inteiro)
Var
    num, count, i: inteiro
    ePrimo: logico
Inicio
    se (age > 1) entao
        num <- age
    senao
        num <- 2
    fimse

    count <- 1

    enquanto (count <= length) faca
        ePrimo <- verdadeiro
        para i de 2 ate (num - 1) faca
            se (num % i = 0) entao
                ePrimo <- falso
            fimse
        fimpara
        se (ePrimo) entao
            sequence[count] <- num
            count <- count + 1
        fimse
        num <- num + 1
    fimenquanto
FimProcedimento

Procedimento evenNumbersFromDay(day, length: inteiro)
Var
    start, count: inteiro
Inicio
    se (day % 2 = 0) entao
        start <- day
    senao
        start <- day - 1
    fimse

    count <- 1
    enquanto (count <= length) faca
        sequence[count] <- start
        start <- start + 2
        count <- count + 1
    fimenquanto
FimProcedimento

Procedimento oddNumbersFromMonth(month, length: inteiro)
Var
    start, count: inteiro
Inicio
    se (month % 2 = 1) entao
        start <- month
    senao
        start <- month + 1
    fimse

    count <- 1
    enquanto (count <= length) faca
        sequence[count] <- start
        start <- start + 2
        count <- count + 1
    fimenquanto
FimProcedimento

Procedimento mainEx1()
Inicio

      escreval("Escolha uma opção:")
    escreval("FB. Fibonnaci")
    escreval("FT. Fettucine")
    escreval("PR. Prime Numbers")
    escreval("EN. Even Numbers")
    escreval("ON. Odd Numbers")

    escreva("Digite a opção desejada (FB, FT, PR, EN, ON): ")
    leia(opcoes)
    
    idade <- 19
    
    se opcoes = "FB" entao
           escreval("Fibonacci:")
          fibonacciSequence(idade)
          para i de 1 ate idade faca
              escreva(sequence[i], " ")
          fimpara
    fimse
    se opcoes = "FT" entao
          escreval("Fettuccine:")
          fettuccineSequence(9, 28, idade)
          para i de 1 ate idade faca
              escreva(sequence[i], " ")
          fimpara
       fimse
    se opcoes = "PR" entao
       escreval("Primos:")
       primeNumbersFromAge(idade, idade)
       para i de 1 ate idade faca
           escreva(sequence[i], " ")
       fimpara
    fimse
    se opcoes = "EN" entao
       escreval("Pares:")
       evenNumbersFromDay(28, idade)
       para i de 1 ate idade faca
           escreva(sequence[i], " ")
       fimpara
    fimse
    se opcoes = "ON" entao
       escreval("Ímpares:")
       oddNumbersFromMonth(9, idade)
       para i de 1 ate idade faca
           escreva(sequence[i], " ")
       fimpara
       escreval("")
    fimse
FimProcedimento

Inicio
    mainEx1()
Fimalgoritmo